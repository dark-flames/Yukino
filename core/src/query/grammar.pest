// Basic
query = {SOI ~ query_content ~ EOI}

query_content = { select_query }

WHITESPACE = _{ " " | "\n" | "\r" }

// Keyword
keyword = _{
    keyword_select | keyword_from | keyword_as | keyword_where | keyword_group_by | order_asc | order_desc |
    keyword_group_by | keyword_having | keyword_order_by | keyword_limit | keyword_offset | keyword_join | keyword_on |
    keyword_left | keyword_right | keyword_full | keyword_inner | keyword_natural | bool_not | bool_and | bit_or |bool_xor
}
keyword_select = _{ ^"select" }
keyword_from = _{ ^"from" }
keyword_as = _{ ^"as" }
keyword_where = _{ ^"where" }
keyword_group_by = _{ ^"group" ~ ^"by" }
keyword_having = _{ ^"having" }
keyword_order_by = _{ ^"order" ~ ^"by" }
keyword_limit = _{ ^"limit" }
keyword_offset = _{ ^"offset" }
keyword_join = _{ ^"join" }
keyword_on = _{ ^"on" }
keyword_left = @{ ^"left" }
keyword_right = @{ ^"right" }
keyword_full = @{ ^"full" }
keyword_inner = @{ ^"inner" }
keyword_outer = @{ ^"outer"}
keyword_natural = _{ ^"natural" }
keyword_cross = _{ ^"cross" }

// Ident

any_ident = @{
    ((ASCII_ALPHA ~ ("_" | ASCII_ALPHA | ASCII_ALPHANUMERIC)*) |
            ("_"+ ~ (ASCII_ALPHA|ASCII_ALPHANUMERIC)+ ~ ("_"|ASCII_ALPHA|ASCII_ALPHANUMERIC)*))
}

ident = @{
    !(keyword  ~ WHITESPACE) ~ ((ASCII_ALPHA ~ ("_" | ASCII_ALPHA | ASCII_ALPHANUMERIC)*) |
        ("_"+ ~ (ASCII_ALPHA|ASCII_ALPHANUMERIC)+ ~ ("_"|ASCII_ALPHA|ASCII_ALPHANUMERIC)*))
}

// Operator
plus  = { "+" }
minus = { "-" }
multi = { "*" }
div = { "/"  }
modulo = { "%" }
left_shift = { "<<"}
right_shift = { ">>" }
bit_and = { "&" }
bit_or = { "|" }
bit_xor = { "^" }
bit_reverse = {  "~" }

compare_operator = _{ bigger_than_or_equal | less_than_or_equal | not_equal | bigger_than | less_than | equal }
less_than = { "<" }
less_than_or_equal = { "<=" }
bigger_than = { ">" }
bigger_than_or_equal = { ">=" }
equal = { "=" }
not_equal = { "!=" | "<>" }

bool_and = { ^"and"}
bool_or = { !(^"order") ~ ^"or"}
bool_xor = { ^"xor"}
bool_not = { ^"not"}

paren_left = _{ "(" }
paren_right = _{ ")" }

comma = _{ "," }

// Literal
literal = { bool| float | int | string | external_ident | null }
/// null
null = { ^"null"}
/// boolean
bool_true  = @{ ^"true"}
bool_false = @{ ^"false"}
bool  = { bool_true | bool_false }
/// int
digit = _{ '0'..'9' }
unsigned_int = @{ digit ~ (digit)* }
int = @{ "-"? ~ unsigned_int }

/// float
exp   = @{ ^"e" ~ int }
float = @{
    int ~ "." ~ unsigned_int? ~ exp? |
    int ~ exp
}
/// string
char_item = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
string_inner = @{ char_item* }
string = {"\"" ~ string_inner ~ "\""}
/// external
external_ident = {"$" ~ ident}

unknown_type = _{ column_ident | external_ident | function_call }

// Function

function_call = { ident ~ paren_left ~ ((expr ~ comma)* ~ expr)? ~ paren_right }

// Expression

expr_factor = { literal | function_call | column_ident | (paren_left  ~ expr ~ paren_right)}
bit_reverse_expr = { expr_factor | (bit_reverse ~ bit_reverse_expr) }
bit_xor_expr = { bit_reverse_expr ~ ( bit_xor ~ bit_xor_expr)* }
term_expr = { bit_xor_expr ~ ((multi | div | modulo) ~ term_expr)* }
add_expr = { term_expr ~ ((plus | multi) ~ add_expr)* }
bit_shift_expr = { add_expr ~ ((left_shift | right_shift) ~ bit_shift_expr)* }
bit_and_expr = { bit_shift_expr ~ (bit_and ~ bit_and_expr)* }
bit_or_expr = { bit_and_expr ~ (bit_or ~ bit_or_expr)* }
cmp_expr = { bit_or_expr ~ (compare_operator ~ bit_or_expr)* }
not_expr = { cmp_expr | (bool_not ~ not_expr) }
and_expr = { not_expr ~ (bool_and ~ not_expr)* }
xor_expr = { and_expr ~ (bool_xor ~ and_expr)* }
expr = { xor_expr ~ (bool_or ~ xor_expr)* }


// Clauses
database_ident = _{ ("\"" ~ any_ident ~ "\"") | ident  }
any = {"*"}
column_ident = { any | database_ident ~ ("." ~ database_ident)* ~  ("." ~ any)?}

table_alias = _{ (keyword_as)? ~ database_ident }
expr_alias = _{ (keyword_as)? ~ database_ident }

/// From clause
table_reference = {database_ident ~ table_alias?}
from_clause = { keyword_from ~ table_reference ~ join_clause* }

/// Join clause
join_type = {((keyword_left | keyword_right | keyword_full) ~ keyword_outer?) | keyword_inner?}
cross_join = { keyword_cross ~ keyword_join ~ table_reference }
natural_join = { keyword_natural ~ join_type ~ keyword_join ~ table_reference }
join_on = { join_type ~ keyword_join ~ table_reference ~ keyword_on ~ expr }

join_clause = { cross_join | natural_join | join_on }

/// Where clause
where_clause = _{ keyword_where ~ expr }

/// GroupBy clause
group_by_clause = { keyword_group_by ~ expr ~ having_clause? }

/// Having clause
having_clause = _{ keyword_having ~ expr }

/// Select clause
select_item = { expr ~ expr_alias? }
select_clause = { keyword_select ~ select_item ~ (comma ~ select_item)* }

/// Order clause
order_asc = @{ ^"asc" }
order_desc = @{ ^"desc" }
order = { order_asc | order_desc }
order_by_clause = { keyword_order_by ~ expr ~ order ~ (comma ~ expr ~ order)* }

/// Limit clause
limit_clause = { keyword_limit ~ int }

/// Offset clause
offset_clause = { keyword_offset ~ int }

// Query

select_query = {
    select_clause ~ from_clause ~ where_clause? ~ group_by_clause? ~ order_by_clause? ~ limit_clause? ~ offset_clause?
}